import com.lagradost.cloudstream3.gradle.CloudstreamExtension
import com.android.build.api.dsl.LibraryExtension
import org.gradle.kotlin.dsl.register

buildscript {
    repositories {
        // Locally-built cloudstream gradle plugin (see CI: built from
        // recloudstream/gradle source via publishToMavenLocal). Must come
        // first so it wins over remote repos.
        mavenLocal()
        google()
        mavenCentral()
        // Shitpack repo which contains our tools and dependencies.
        // NOTE (Sep 2026): JitPack's artifacts for recloudstream/gradle are
        // corrupt server-side (snapshot timestamp "aster-<sha>", stub poms,
        // 292-byte jars), so the plugin is built from source instead — see
        // .github/workflows/build.yml. Other JitPack artifacts (jadb, …)
        // are unaffected; POM-only resolution guards against the same
        // class of broken Gradle module metadata.
        maven("https://jitpack.io") {
            metadataSources {
                mavenPom()
                artifact()
            }
        }
    }

    dependencies {
        classpath("com.android.tools.build:gradle:9.1.1")
        // Cloudstream gradle plugin which makes everything work and builds plugins.
        // Built from source in CI (recloudstream/gradle @ 32895aed) — do NOT
        // switch back to com.github.recloudstream:gradle:master-SNAPSHOT
        // until JitPack serves valid artifacts for it again.
        classpath("com.lagradost.cloudstream3:gradle:local-SNAPSHOT")
        classpath("org.jetbrains.kotlin:kotlin-gradle-plugin:2.3.20")
    }
}

allprojects {
    repositories {
        google()
        mavenCentral()
        maven("https://jitpack.io") {
            metadataSources {
                mavenPom()
                artifact()
            }
        }
    }
}

fun Project.cloudstream(configuration: CloudstreamExtension.() -> Unit) = extensions.getByName<CloudstreamExtension>("cloudstream").configuration()

fun Project.android(configuration: LibraryExtension.() -> Unit) {
    extensions.getByName<LibraryExtension>("android").apply {
        (extensions.findByName("java") as? JavaPluginExtension)?.apply {
            // Use Java 17 toolchain even if a higher JDK runs the build.
            // We still use Java 8 for now which higher JDKs have deprecated.
            toolchain {
                languageVersion.set(JavaLanguageVersion.of(17))
            }
        }

        configuration()
    }
}

subprojects {
    if (project.path == ":extensions") return@subprojects

    apply(plugin = "com.android.library")
    apply(plugin = "com.lagradost.cloudstream3.gradle")

    cloudstream {
        // when running through gitHub workflow, GITHUB_REPOSITORY should contain current repository name
        setRepo(System.getenv("GITHUB_REPOSITORY") ?: "JamesBonner/NTV-Cloudstream")

        authors = listOf("JamesBonner")
    }

android {
        namespace = "com.ntv"

        compileSdk = 36

        defaultConfig {
            minSdk = 21
        }
        compileOptions {
            sourceCompatibility = JavaVersion.VERSION_1_8
            targetCompatibility = JavaVersion.VERSION_1_8
        }
        }

        tasks.withType<org.jetbrains.kotlin.gradle.tasks.KotlinJvmCompile> {
            compilerOptions {
                jvmTarget.set(org.jetbrains.kotlin.gradle.dsl.JvmTarget.JVM_1_8)
                freeCompilerArgs.addAll(
                    listOf(
                        "-Xno-call-assertions",
                        "-Xno-param-assertions",
                        "-Xno-receiver-assertions",
                        "-Xannotation-default-target=param-property"
                    )
                )
            }
        }

    dependencies {
        val cloudstream by configurations
        val implementation by configurations

        // Stubs for all Cloudstream classes
        cloudstream("com.lagradost:cloudstream3:pre-release")

        // these dependencies can include any of those which are added by the app,
        // but you don't need to include any of them if you don't need them
        // https://github.com/recloudstream/cloudstream/blob/master/app/build.gradle
        implementation(kotlin("stdlib"))
        implementation("com.github.Blatzar:NiceHttp:0.4.13")
        implementation("org.jsoup:jsoup:1.22.1")
        implementation("com.fasterxml.jackson.module:jackson-module-kotlin:2.13.5")
        implementation("com.fasterxml.jackson.core:jackson-databind:2.13.5")
        implementation("org.jetbrains.kotlinx:kotlinx-coroutines-android:1.10.2")
        implementation("org.jetbrains.kotlinx:kotlinx-serialization-json:1.7.3")
        implementation("org.mozilla:rhino:1.9.0")
        implementation("me.xdrop:fuzzywuzzy:1.4.0")
        implementation("com.google.code.gson:gson:2.13.2")
        implementation("app.cash.quickjs:quickjs-android:0.9.2")
        implementation("com.github.vidstige:jadb:1.2.1")
    }
}


tasks.register<Delete>("clean") {
    delete(rootProject.layout.buildDirectory)
}
tasks.register("derle") {
    group = "help"
    doLast {
        println("Filtreleme modu aktif: status=0 olan eklentiler derleme disinda birakildi.")
    }
}
